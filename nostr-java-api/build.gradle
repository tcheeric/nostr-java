plugins {
    id 'org.springframework.boot'
    id 'org.gradle.test-retry'
    id 'nostr-java.conventions'
}

description = 'nostr-java-api'

dependencies {
    api project(':nostr-java-client')
    api project(':nostr-java-encryption')
    api project(':nostr-java-id')
    api project(':nostr-java-event')
    api project(':nostr-java-util')
    api project(':nostr-java-crypto')

    implementation "org.springframework.boot:spring-boot-starter:${rootProject.property('nostr-java.springBootVersion')}"
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    compileOnly "org.projectlombok:lombok:${rootProject.property('nostr-java.lombok')}"
    annotationProcessor "org.projectlombok:lombok:${rootProject.property('nostr-java.lombok')}"
    testCompileOnly "org.projectlombok:lombok:${rootProject.property('nostr-java.lombok')}"
    testAnnotationProcessor "org.projectlombok:lombok:${rootProject.property('nostr-java.lombok')}"

    implementation 'org.slf4j:slf4j-api'

    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation "org.springframework.boot:spring-boot-starter-test:${rootProject.property('nostr-java.springBootVersion')}"
    testImplementation 'com.google.guava:guava:33.4.0-jre'
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

tasks.named('bootJar') { enabled = false }
tasks.named('jar') { archiveClassifier = '' }

tasks.register('apiIntegrationTest', Test) {
    description = 'api integration tests.'
    group = 'verification'
    systemProperty("spring.profiles.active", "test")
    useJUnitPlatform()
    filter {
        excludeTestsMatching("nostr.api.unit.*");
        includeTestsMatching("nostr.api.integration.*");
    }
}

apiIntegrationTest {
    retry {
        failOnPassedAfterRetry = false
        maxRetries = 1
    }
}

check.dependsOn apiIntegrationTest
